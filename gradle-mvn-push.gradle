apply plugin: 'maven'
apply plugin: 'signing'

version = buildConfig.versionName
group = GROUP

def isReleaseBuild() {
  return version.contains("SNAPSHOT") == false
}

//region repository config
def getReleaseRepositoryUrl() {
  return hasProperty('RELEASE_REPOSITORY_URL') ? RELEASE_REPOSITORY_URL : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

def getSnapshotRepositoryUrl() {
  return hasProperty('SNAPSHOT_REPOSITORY_URL') ? SNAPSHOT_REPOSITORY_URL : "https://oss.sonatype.org/content/repositories/snapshots/"
}

def getRepositoryUsername() {
  return hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : ""
}

def getRepositoryPassword() {
  return hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : ""
}

//endregion

//region android/kotlin/java artifact
def kotlinAndroidArtifactTasks() {
  if (!project.plugins.hasPlugin('org.jetbrains.dokka-android')) {
    throw new GradleException("Apply the dokka-android plugin in ${project.name}")
  }

  dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/docs/kdoc"
    includes = ['module.md']
    classpath = new ArrayList<File>(project.tasks['assemble'].outputs.files.files)
    // todo check main.java / main.kotlin
    sourceDirs = android.sourceSets.main.java.srcDirs
  }

  task docJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from dokka.outputDirectory
  }

  task sourceJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
  }
}

def androidArtifactTasks() {
  task androidJavadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs

    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    exclude '**/internal/*'

    if (JavaVersion.current().isJava8Compatible()) {
      options.addStringOption('Xdoclint:none', '-quiet')
    }
  }

  task docJar(type: Jar, dependsOn: androidJavadoc) {
    classifier = 'javadoc'
    from androidJavadoc.destinationDir
  }

  task sourceJar(type: Jar) {
    classifier = 'sources'
    // todo check main.java / main.kotlin
    from android.sourceSets.main.java.sourceFiles
  }
}

def kotlinArtifactTasks() {
  if (!project.plugins.hasPlugin('org.jetbrains.dokka')) {
    throw new GradleException("Apply the dokka plugin in ${project.name}")
  }

  dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/docs/kdoc"
    includes = ['module.md']
    sourceDirs = sourceSets.main.allSource
  }

  task docJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from dokka.outputDirectory
  }

  task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }
}

def javaArtifactTasks() {
  task javaJavadoc(type: Javadoc) {
    source = sourceSets.main.allSource

    if (JavaVersion.current().isJava8Compatible()) {
      options.addStringOption('Xdoclint:none', '-quiet')
    }
  }

  task docJar(type: Jar, dependsOn: javaJavadoc) {
    classifier = 'javadoc'
    from javaJavadoc.destinationDir
  }

  task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource.srcDirs
  }
}

//endregion

afterEvaluate { project ->
  uploadArchives {
    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: getReleaseRepositoryUrl()) {
          authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
        }
        snapshotRepository(url: getSnapshotRepositoryUrl()) {
          authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
        }

        //region pom
        pom.groupId = GROUP
        pom.artifactId = POM_ARTIFACT_ID
        pom.version = version

        pom.project {
          name POM_NAME
          packaging POM_PACKAGING
          description POM_DESCRIPTION
          url POM_URL

          scm {
            url POM_SCM_URL
            connection POM_SCM_CONNECTION
            developerConnection POM_SCM_DEV_CONNECTION
          }

          licenses {
            license {
              name POM_LICENCE_NAME
              url POM_LICENCE_URL
            }
          }

          developers {
            developer {
              id POM_DEVELOPER_ID
              name POM_DEVELOPER_NAME
            }
          }
        }
        //endregion
      }
    }
  }

  signing {
    required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
  }

  if (hasProperty("AUTOMATIC_MODULE_NAME")) {
    jar {
      manifest {
        attributes('Automatic-Module-Name': properties.get("AUTOMATIC_MODULE_NAME"))
      }
    }
  }

  if (project.getPlugins().hasPlugin('com.android.application') || project.getPlugins().hasPlugin('com.android.library')) {
    task install(type: Upload, dependsOn: assemble) {
      repositories.mavenInstaller {
        configuration = configurations.archives

        //region pom
        pom.groupId = GROUP
        pom.artifactId = POM_ARTIFACT_ID
        pom.version = version

        pom.project {
          name POM_NAME
          packaging POM_PACKAGING
          description POM_DESCRIPTION
          url POM_URL

          scm {
            url POM_SCM_URL
            connection POM_SCM_CONNECTION
            developerConnection POM_SCM_DEV_CONNECTION
          }

          licenses {
            license {
              name POM_LICENCE_NAME
              url POM_LICENCE_URL
            }
          }

          developers {
            developer {
              id POM_DEVELOPER_ID
              name POM_DEVELOPER_NAME
            }
          }
        }
        //endregion
      }
    }

    // if (project.plugins.hasPlugin('kotlin-android')) {
    //   kotlinAndroidArtifactTasks()
    // } else {
    // }
    androidArtifactTasks()
  } else {

    install {
      repositories.mavenInstaller {
        //region pom
        pom.groupId = GROUP
        pom.artifactId = POM_ARTIFACT_ID
        pom.version = version

        pom.project {
          name POM_NAME
          packaging POM_PACKAGING
          description POM_DESCRIPTION
          url POM_URL

          scm {
            url POM_SCM_URL
            connection POM_SCM_CONNECTION
            developerConnection POM_SCM_DEV_CONNECTION
          }

          licenses {
            license {
              name POM_LICENCE_NAME
              url POM_LICENCE_URL
            }
          }

          developers {
            developer {
              id POM_DEVELOPER_ID
              name POM_DEVELOPER_NAME
            }
          }
        }
        //endregion
      }
    }

    if (project.plugins.hasPlugin('kotlin')) {
      kotlinArtifactTasks()
    } else {
      javaArtifactTasks()
    }
  }

  artifacts {
    archives sourceJar
    archives docJar
  }
}
